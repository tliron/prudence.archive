
prudence.include('../libraries/stickstick/data/')

import org.json.JSONObject

handleInit = { resource ->
    resource.addMediaTypeByName('text/plain')
    resource.addMediaTypeByName('application/json')
}

handleGet = { resource ->
    def form = resource.resource.request.resourceRef.queryAsForm
    def fresh = form.getFirstValue('fresh') == 'true'
    
    def maxTimestamp
    def boardList = []
    def notes

    def connection = getConnection(fresh)
    try {
	    def boards = getBoards(connection)
	    if(boards != null) {
		    for(def board in boards) {
		        boardList.push(board.id)
		        def timestamp = board.timestamp
		        if((maxTimestamp == null) || (timestamp > maxTimestamp)) {
		            maxTimestamp = timestamp
		        }
		    }
	    }
	    else {
	        return null
	    }
	
	    notes = getNotes(connection)
    }
    finally {
    	connection.close()
    }

    if(maxTimestamp != null) {
        resource.modificationTimestamp = maxTimestamp
    }
    return new JSONObject([boards: boardList, notes: notes])
}

handleGetInfo = { resource ->
    def connection = getConnection()
    try {
    	return getBoardMaxTimestamp(connection)
    }
    finally {
    	connection.close()
    }
}

handlePut = { resource ->
    // Note: You can only "consume" the entity once, so if we want it
    // as text, and want to refer to it more than once, we should keep
    // a reference to that text.
    
    def text = resource.entity.text
    def entity = new JSONObject(text)
    def note = [:]
	for(def key in entity.keys()) {
		note[key] = entity.get(key)
	}
    
    def connection = getConnection()
    try {
    	addNote(note, connection)
        updateBoardTimestamp(note, connection)
    }
    finally {
    	connection.close()
    }
    
    return handleGet(resource)
}