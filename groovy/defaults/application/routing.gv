//
// Prudence Application Routing
//

import java.lang.ClassLoader
import java.io.File
import org.restlet.routing.Router
import org.restlet.routing.Redirector
import org.restlet.routing.Template
import org.restlet.resource.Finder
import org.restlet.resource.Directory
import com.threecrickets.scripturian.LanguageManager
import com.threecrickets.scripturian.DefrostTask
import com.threecrickets.scripturian.file.DocumentFileSource
import com.threecrickets.prudence.util.PrudenceRouter
import com.threecrickets.prudence.util.PreheatTask

classLoader = ClassLoader.systemClassLoader

//
// Utilities
//

// Makes sure we have slashes where we expect them
fixURL = { url ->
	url = (url =~ /\/\//).replaceAll('/') // no doubles
	if(url.size() > 0 && url[0] == '/') { // never at the beginning
		url = url.substring(1)
	}
	if(url.size() > 0 && url[url.size() - 1] != '/') { // always at the end
		url += '/'
	}
	return url
}

//
// Internal router
//

component.internalRouter.attach('/' + applicationInternalName + '/', application).matchingMode = Template.MODE_STARTS_WITH

//
// Hosts
//
// Note that the application's context will not be created until we attach the application to at least one
// virtual host. See component/hosts.gv for more information.
//

addTrailingSlash = new Redirector(application.context, '{ri}/', Redirector.MODE_CLIENT_PERMANENT)

print(application.name + ': ')
hosts.eachWithIndex() { entry, i ->
	host = entry[0]
	url = entry[1]
	if(!url) {
		url = applicationDefaultURL
	}
	print('"' + url + '" on ' + host.name)
	host.attach(url, application).matchingMode = Template.MODE_STARTS_WITH
	if(url != '/') {
		if(url[url.size() - 1] == '/') {
			url = url.substring(0, url.size() - 1)
		}
		host.attach(url, addTrailingSlash).matchingMode = Template.MODE_EQUALS
	}
	if(i < hosts.size() - 1) {
		print(', ')
	}
}
println('.')

attributes = application.context.attributes

//
// Inbound root
//

router = new PrudenceRouter(application.context)
router.routingMode = Router.MODE_BEST_MATCH
application.inboundRoot = router

//
// Add trailing slashes
//

for(url in urlAddTrailingSlash) {
	url = fixURL(url)
	if(url.size() > 0) {
		if(url[url.size() - 1] == '/') {
			// Remove trailing slash for pattern
			url = url.slice(0, -1)
		}
		router.attach(url, addTrailingSlash)
	}
}

//
// Dynamic web
//

languageManager = new LanguageManager()
dynamicWebDocumentSource = new DocumentFileSource(applicationBasePath + dynamicWebBasePath, dynamicWebDefaultDocument, dynamicWebMinimumTimeBetweenValidityChecks)
attributes.put('com.threecrickets.prudence.GeneratedTextResource.languageManager', languageManager)
attributes.put('com.threecrickets.prudence.GeneratedTextResource.defaultLanguageTag', 'groovy')
attributes.put('com.threecrickets.prudence.GeneratedTextResource.defaultName', dynamicWebDefaultDocument)
attributes.put('com.threecrickets.prudence.GeneratedTextResource.documentSource',dynamicWebDocumentSource)
attributes.put('com.threecrickets.prudence.GeneratedTextResource.sourceViewable', dynamicWebSourceViewable)

dynamicWeb = new Finder(application.context, classLoader.loadClass('com.threecrickets.prudence.GeneratedTextResource'))
router.attachBase(fixURL(dynamicWebBaseURL), dynamicWeb)

if(dynamicWebDefrost) {
	defrostTasks = DefrostTask.forDocumentSource(dynamicWebDocumentSource, languageManager, true)
	for(defrostTask in defrostTasks) {
		tasks.push(defrostTask)
	}
}

//
// Static web
//

staticWeb = new Directory(router.context, new File(applicationBasePath + staticWebBasePath).toURI().toString())
staticWeb.listingAllowed = staticWebDirectoryListingAllowed
staticWeb.negotiatingContent = true
router.attachBase(fixURL(staticWebBaseURL), staticWeb)

//
// Resources
//

resourcesDocumentSource = new DocumentFileSource(applicationBasePath + resourcesBasePath, resourcesDefaultName, resourcesMinimumTimeBetweenValidityChecks)
attributes.put('com.threecrickets.prudence.DelegatedResource.languageManager', languageManager)
attributes.put('com.threecrickets.prudence.DelegatedResource.defaultLanguageTag', 'groovy')
attributes.put('com.threecrickets.prudence.DelegatedResource.defaultName', resourcesDefaultName)
attributes.put('com.threecrickets.prudence.DelegatedResource.documentSource', resourcesDocumentSource)
attributes.put('com.threecrickets.prudence.DelegatedResource.sourceViewable', resourcesSourceViewable)

resources = new Finder(application.context, classLoader.loadClass('com.threecrickets.prudence.DelegatedResource'))
router.attachBase(fixURL(resourcesBaseURL), resources)

if(resourcesDefrost) {
	defrostTasks = DefrostTask.forDocumentSource(resourcesDocumentSource, languageManager, true)
	for(defrostTask in defrostTasks) {
		tasks.push(defrostTask)
	}
}

//
// Preheat
//

if(dynamicWebPreheat) {
	preheatTasks = PreheatTask.forDocumentSource(dynamicWebDocumentSource, component.context, applicationInternalName)
	for(preheatTask in preheatTasks) {
		tasks.push(preheatTask)
	}
}

for(preheatResource in preheatResources) {
	tasks.push(new PreheatTask(component.context, applicationInternalName, preheatResource))
}
